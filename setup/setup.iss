; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Seppuku"
#define MyAppVersion "0.7"
#define MyAppPublisher "Ricardo Tondello;Gutierry Antonio Neto Pereira - MP - 2018"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3CCE50C0-23A2-46CF-9C99-C370BDECEB23}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName=C:\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=seppuku_{#MyAppVersion}_setup
Compression=lzma
SolidCompression=yes
PrivilegesRequired=lowest

[Languages]
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

[code]
procedure CurStepChanged(CurStep: TSetupStep);
var
    ResultCode: Integer;
    wmicommand: string;
begin
    // before installing any file
    if CurStep = ssInstall then
    begin
        wmicommand := ExpandConstant('PROCESS WHERE "ExecutablePath like ''{app}\%%''" DELETE');

        StringChangeEx(wmicommand, '\', '\\', True);

        Exec('WMIC', wmicommand, '', SW_HIDE, ewWaitUntilTerminated, ResultCode);
    end;
end;

[Tasks]
Name: TaskRegistry; Description: "Configurar TFS para abrir o Seppuku após instalação?";
Name: TaskExecutarAoIniciar; Description: "Iniciar seppuku com o windows? (aconselhável)";


[Files]
Source: "dist\seppuku.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\settings.json"; DestDir: "{app}"; Flags: onlyifdoesntexist
Source: "..\templates\*"; DestDir: "{app}\templates"; Flags: ignoreversion
Source: "..\static\*";    DestDir: "{app}\static";    Flags: ignoreversion recursesubdirs

Source: "SourceMonitor\SourceMonitor.exe"; DestDir: "{app}"; Flags: onlyifdoesntexist
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
Filename: "{app}\seppuku.exe"; Flags: postinstall nowait

[Registry]
Root: HKCU; Subkey: "Software\Microsoft\VisualStudio\10.0\TeamFoundation\SourceControl\DiffTools\.pas\Compare"; ValueType: string; ValueName: "Command"; ValueData: "{app}\seppuku.exe"; Tasks: TaskRegistry
Root: HKCU; Subkey: "Software\Microsoft\VisualStudio\10.0\TeamFoundation\SourceControl\DiffTools\.pas\Compare"; ValueType: string; ValueName: "Arguments"; ValueData: "%1 %2"; Tasks: TaskRegistry

; executa ao iniciar o windows
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: "Seppuku"; ValueData: """{app}\seppuku.exe"""; Flags: uninsdeletevalue; Tasks: TaskExecutarAoIniciar














